import platform
import os

def get_boost_python_filename():
	for file in os.listdir(os.getenv('BOOSTPATH') + '\\stage\\lib'):
		if file.startswith('boost_python') and file.endswith('.dll'):
			return file
	raise Exception('boost_python DLL was not found!')

Import('*')

source = Glob('*.cpp')

# Build shared library
shared_lib_bin = env.SharedLibrary('classifier', source)

if platform.system() == "Windows":
	# Copy shared library to Django directory
	shared_lib = Command('#Django/med/utility/classifier.pyd', shared_lib_bin, Copy("$TARGET", "$SOURCE"))

	# Copy Dll and set as a dependency
	dll_filename = get_boost_python_filename()
	dll = Command(
		'#Django/med/utility/' + dll_filename, 
		os.getenv('BOOSTPATH') + '\\stage\\lib\\' + dll_filename, 
		Copy("$TARGET", "$SOURCE")
	)
	Depends(shared_lib, dll)
else:
	# Copy shared library to Django directory
	shared_lib = Command('#Django/med/utility/classifier.so', shared_lib_bin, Copy("$TARGET", "$SOURCE"))

# Common dependencies
Depends(shared_lib, shared_lib_bin)

Return('shared_lib')
