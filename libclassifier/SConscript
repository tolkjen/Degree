import platform
import os

def get_boost_python_filename():
	for file in os.listdir(os.getenv('BOOSTPATH') + '\\stage\\lib'):
		if file.startswith('boost_python') and file.endswith('.dll'):
			return file
	raise Exception('boost_python DLL was not found!')

# Set environment variables according to OS platform
env = Environment(TARGET_ARCH = 'x86')
if platform.system() == "Windows":
	env.Append(LIBPATH = [
		os.getenv('BOOSTPATH') + '\\stage\\lib', 
		os.getenv('PYTHONPATH') + '\\libs'
	])
	env.Append(CPPPATH = [
		os.getenv('BOOSTPATH'), 
		os.getenv('PYTHONPATH') + '\\include'
	])
	env.Append(CPPPATH = ['#libclassifier/src', '#libclassifier/src/lib'])
	env.Append(CCFLAGS = ['/MD', '/EHsc'])
else:
	env.Append(CPPPATH = ['#libclassifier/src', '#libclassifier/src/lib'])
	env.ParseConfig('pkg-config --cflags --libs python')
	env.Append(LIBS=['boost_python', 'boost_system'])
	env.Append(LINKFLAGS=['-fPIC'])
	env.Append(CCFLAGS=['-std=c++11'])

# Add more SConscripts
shared_lib = SConscript('src/lib/SConscript', 
	exports = ['env', 'get_boost_python_filename'], 
	variant_dir = 'bin/lib',
	duplicate = 0)
shared_lib_tests = SConscript('src/lib/SConscriptTests', 
	exports = ['env', 'get_boost_python_filename'], 
	variant_dir = 'bin/unittests',
	duplicate = 0)

# Aliases
Alias('lib', shared_lib)

# Cleanup
Clean(shared_lib, 'bin/lib')
Clean(shared_lib_tests, 'bin/unittests')
Clean([shared_lib, shared_lib_tests], 'bin')

Return('shared_lib', 'shared_lib_tests')
